import as "glass" from "packages"
import "window_tools" as "wt"

permission "request" "rotur token"

token = roturToken()

posts = {"got":false,"feed":"Loading"}

api = "https://claw.rotur.dev/"

url = "feed"

users = {}

attachment = ""
user_profile = ""

proxy = "https://apps.mistium.com/cors?url="

window "set_dragbox" [[2,2,30,0],[-2,2,-100,-40]]
window "dimensions" 1200 700
window "min_size" 800 700

def "send_post" "this.content,this.attachment" (
  this.url = "https://claw.rotur.dev/post?os=originOS&auth=" ++ token
  this.url += "&content=" ++ encodeURIComponent(this.content)
  if this.attachment != "" (
    this.url += "&attachment=" ++ encodeURIComponent(this.attachment)
  )
  return this.url.getAsync()
)

def "delete_post" "this.id" (
  this.url = "https://claw.rotur.dev/delete?auth=" ++ token ++ "&id=" ++ this.id
  return this.url.getAsync()
)

def "set_rating" "this.id, this.rating" (
  this.url = "https://claw.rotur.dev/rate?auth=" ++ token ++ "&id=" ++ this.id ++ "&rating=" ++ this.rating.toNum()
  return this.url.getAsync()
)

def "load_profile" "this.username" (
  posts.got = false
  posts.feed = []
  url = "profile?auth=" ++ token ++ "&name=" ++ this.username
  hovered_post = ""
)

def "render_post" "this.w, this.h, this.post, this.offset_y, this.reply" (
  this.user = this.post.user.toLower()
  loc 9999 2 0 y
  square this.w this.h 10 : c#window_colour
  if mouse_touching (
    square this.w this.h 15 : c#seco
    square this.w this.h 10 : c#prim
    square this.w this.h - 70 10 0 1
    if onclick and this.reply.not (
      posts.feed = this.post.replies
      posts.data = this.post
      url = "Post_" ++ this.post.id
    )
  )
  loc 2 2 33 y + this.offset_y
  
  if users[this.user] == "" (
    this.val = (api ++ "profile?name=" ++ this.user).getAsync()
    if this.val != "Loading" (
      users[this.user] = this.val
    )
  )
  this.user = users[this.user]
  
  image this.user.pfp 40 40
  text this.user.username 10 : c#txtc chx#30
  if onclick (
    load_profile this.user.username
  )
  
  this.time = this.post.timestamp
  if timestamp - this.time > 12800000 (
    this.time = this.time.timestamp("convert-date")
  ) else (
    this.time = this.time.timestamp("convert-relative")
  )
  this.tw = this.time.len * 8
  loc -2 2 -20 - this.tw y + this.offset_y
  text this.time 8
  
  if this.reply.not (
    loc 2 2 20 y - this.offset_y
    text "Posted From " ++ (this.post.os ?? "Unknown") 8
      
    loc -2 2 -30 y - this.offset_y
    this.likes = this.post.likes
    if this.likes == "" (
      this.total_likes = 0
      this.i_liked = false
    ) else (
      this.total_likes = this.likes.len
      this.i_liked = this.likes.contains(username.toLower())
    )
    icon this.i_liked ? "favorites-full" "favorites" 0.8
    if onclick (
      feed[count].likes ??= []
      if this.i_liked (
        feed[count].likes = feed[count].likes.delete(username.toLower())
        this.rating = 0
      ) else (
        feed[count].likes = feed[count].likes.append(username.toLower())
        this.rating = 1
      )
      set_rating this.post.id this.rating
      posts.feed = feed
    )

    change_x this.total_likes.len * -8 - 20
    text this.total_likes 8
    
    change_x -40
    icon "w 4 cutcircle 0 -5 10 4 45 w 4.1 line 10 -10 10 -5 line 0 5 -10 5 cont -5 0 line -10 5 -5 10" 0.7
    this.total_replies = (this.post.replies ?? []).len
    change_x this.total_replies.len * -8 - 20
    text this.total_replies 8
  )
  if this.post.attachment == "" (
    this.offset = 0
  ) else (
    this.offset = 3
    loc -2 2 -30 y
    icon "image" 0.8
  )
  loc 2 2 20 y + 20
  text this.post.content.replace("\n"," ").wrapText(this.w / 9 - 1 - this.offset) 9
)

def "render_main" "feed" (
  this.ih = 180
    
  len = feed.len
  count = (scroll_yfeed // this.ih).clamp(0,100)
  y = scroll_yfeed % this.ih
  y -= this.ih / 2
  
  this.w = frame.width - 25
  loop (window_height // this.ih + 1).clamp(0,len) (
    count ++
    if count > len (
      return
    )
    this.h = this.ih - 20
    
    render_post this.w this.h feed[count] (this.h / 2 - 20) url.startsWith("Post_")
    y -= this.ih
  )
)

def "side_button" "this.icon, this.txt, this.callback" (
  square 450 0 30 0 1
  if mouse_touching (
    cursor "pointer"
    if onclick (
      return this.callback()
    )
  )
  icon this.icon 0.7 : c#txtc
  change_x this.icon == "" ? -15 30
  
  text this.txt 10 : c#txtc
)

def "claw_input" "this.y, this.w, this.onsend, this.message" (
  goto x this.y
  square this.w 90 10 : c#window_colour

  change_y 25
  input this.w - 15 23 "claw" this.message
  input_claw.trim(0,100)
  change this.w / -2 + 10 -50
  text input_claw.len ++ "/100" 8 : c#txtc

  change_x 40
  text "Click Here To Paste An Image URL" 8
  if onclick (
    attachment = clipboard
    if attachment.startsWith("https://").not (
      attachment = ""
      say "Invalid, must be https:// image"
    )
  )
  if attachment != "" (
    image attachment 30 30 : chx#30
  )
  goto 115 + (this.w / 2) this.y - 25
  icon "send" 0.8
  if onclick (
    this.onsend()
  )
)

mainloop:
if url.startsWith("Post_") (
  wt:load_theme
  window_accent = user.theme.accent
  post_id = url.split("_")[2]
) else if url == "feed" or url.startsWith("following_feed") (
  wt:load_theme
  window_accent = user.theme.accent
) else (
  theme = posts.data.theme
  prim = theme.primary
  seco = theme.secondary
  tert = theme.tertiary
  txtc = theme.text
  window_accent = theme.accent
  window_colour = theme.background
)

w = window

glass:frame w.left w.top w.right w.bottom
  goto 0 0
  pen "size" w.width + w.height
  c window_colour
  pen "opacity" 50
  pen "down"
  pen "up"
frame "clear"

top = w.top - 170
x = w.width > 1100 ? 135 0

if posts.got (
  c prim
  len = posts.feed.len
  
  if url == "feed" (
    claw_input w.top - 110 790 def() -> (
      send_post input_claw attachment
      input_claw = ""
      attachment = ""
      posts.got = false
      posts.feed = []
    ) "Type your post..."
  ) else if url.startsWith("Post_") (
    top -= 190
    
    frame x - 408 w.top - 50 x + 408 top - 110
      y = -100
      render_post 790 170 posts.data 65
    frame "clear"
    
    claw_input w.top - 300 790 def() -> (
      c (api ++ "reply?id=" ++ posts.data.id ++ "&auth=" ++ token ++ "&content=" ++ encodeURIComponent(input_claw)).getAsync()
      input_claw = ""
      attachment = ""
    ) "Reply to this post..."
  ) else (
    top = w.top - 60
  )
  
  c prim

  frame x - 408 top x + 408 w.bottom len * 180 "feed"
    render_main posts.feed
  frame "clear"
) else if url.startsWith("profile?name=") and users.contains(url.split("=").last()) (
  name = url.split("=").last()
  posts.feed = users[name].posts
  posts.data = users[name].delete("posts")
) else (
  posts.feed = (api ++ url).getAsync()
  if posts.feed != "Loading" (
    posts.got = true
    if url.startsWith("profile?") (
      posts.data = posts.feed.delete("posts")
      posts.feed = posts.feed.posts
    )

  )
)

if user_profile == "" (
  user_profile = (api ++ "profile?name=" ++ username).getAsync()
  if user_profile == "Loading" (
    user_profile = ""
  )
)

x = w.left + 25

goto w.left + 120
square 300 w.height 10 : c#window_colour

if url.startsWith("Post_") (
  loc 2 2 25 -75
  side_button "image" + posts.data.pfp + "40" posts.data.username def() -> (
    load_profile username
  )

  set_x x : chy#-40
  side_button "" "Followers:" + posts.data.followers def() -> ()

  set_x x : chy#-30
  side_button "" "Following:" + posts.data.following def() -> ()

  set_x x : chy#-50
  side_button "left-arrow" "Back To Feed" def() -> (
    posts.got = false
    posts.feed = []
    url = "feed"
  )
) else (
  if url.startsWith("profile").not (
    loc 2 2 25 -75
    side_button "image" + user_icon + "40" "My Profile" def() -> (
      load_profile username
    )

    set_x x : chy#-40
    side_button "" "Followers:" + user_profile.followers def() -> ()

    set_x x : chy#-30
    side_button "" "Following:" + user_profile.following def() -> ()
  ) else (
    loc 2 2 25 -75
    side_button "image" + posts.data.pfp + "40" posts.data.username def() -> (
      load_profile username
    )

    set_x x : chy#-40
    side_button "" "Followers:" + posts.data.followers def() -> ()

    set_x x : chy#-30
    side_button "" "Following:" + posts.data.following def() -> ()

    set_x x : chy#-50
    side_button "left-arrow" "Back To Feed" def() -> (
      posts.got = false
      posts.feed = []
      url = "feed"
    )
  
    set_x x : chy#-50
    if posts.data.followed (
      side_button "tick" "Unfollow" + posts.data.username def() -> (
        this.url = api ++ "/unfollow?auth=" ++ token ++ "&username=" ++ posts.data.username
        posts.data.followed = false
        return this.url.getAsync()
      )
    ) else (
      side_button "tick" "Follow" + posts.data.username def() -> (
        this.url = api ++ "/follow?auth=" ++ token ++ "&username=" ++ posts.data.username
        posts.data.followed = true
        return this.url.getAsync()
      )
    )
  )
)

set_x x : chy#-50
side_button "reload" "Reload" def() -> (
  posts.got = false
  posts.feed = []
)

goto 0 w.top - 20
square w.width 40 10 : c#window_colour

c txtc
loc 2 2 10 -20
text "Claw" 10

loc 9999 2 20 -20
text "Claw Feed" 10
if onclick (
  posts.got = false
  url = "feed"
)

loc 9999 2 160 -20
text "Following" 10
if onclick (
  posts.got = false
  url = "following_feed?auth=" ++ token
)


import "win-buttons"
