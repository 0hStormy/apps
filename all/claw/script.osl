import as "glass" from "packages"
import "window_tools" as "wt"

permission "request" "rotur token"

token = roturToken()

posts = {
  "got":false,
  "feed":"Loading"
}

api = "https://claw.rotur.dev/"

url = "feed"

users = {}

attachment = ""
user_profile = ""
overlay = ""

proxy = "https://apps.mistium.com/cors?url="

window "set_dragbox" [[2,2,30,0],[-2,2,-100,-40]]
window "dimensions" 1200 700
window "min_size" 800 700

def "send_post" "this.content, this.attachment" (
  local url = "https://claw.rotur.dev/post?os=originOS&auth=" ++ token
  local url ++= "&content=" ++ encodeURIComponent(content)
  if attachment != "" (
    local url ++= "&attachment=" ++ encodeURIComponent(attachment)
  )
  return url.getAsync()
)

def "delete_post" "this.id" (
  local url = "https://claw.rotur.dev/delete?auth=" ++ token ++ "&id=" ++ id
  return url.getAsync()
)

def "set_rating" "this.id, this.rating" (
  local url = "https://claw.rotur.dev/rate?auth=" ++ token ++ "&id=" ++ id ++ "&rating=" ++ rating.toNum()
  return url.getAsync()
)

def "load_profile" "this.username" (
  posts.got = false
  posts.feed = []
  url = "profile?auth=" ++ token ++ "&name=" ++ username
  hovered_post = ""
)

def "render_post" "w, h, post, offset_y, reply" (
  local user = post.user.toLower()
  loc 9999 2 0 y
  square w h 10 : c#window_colour
  if mouse_touching (
    square w h 15 : c#seco
    square w h 10 : c#prim
    square w h - 70 10 0 1
    if onclick and reply.not() (
      posts.feed = post.replies
      posts.data = post
      url = "Post_" ++ post.id
    )
  )
  loc 2 2 33 y + offset_y
  
  image "https://avatars.rotur.dev/" ++ user 40 40
  text user 10 : c#txtc chx#30
  if onclick (
    load_profile user
  )
  
  local time = post.timestamp
  if timestamp - time > 128000000 (
    local time = time.timestamp("convert-date")
  ) else (
    local time = time.timestamp("convert-relative")
  )
  local tw = time.len * 8
  loc -2 2 -20 - tw y + offset_y
  text time 8
  
  if reply.not() (
    loc 2 2 20 y - offset_y
    text "Posted From " ++ (post.os ?? "Unknown") 8
      
    loc -2 2 -30 y - offset_y
    local likes = post.likes
    if likes == "" (
      local total_likes = 0
      local i_liked = false
    ) else (
      local total_likes = likes.len
      local i_liked = likes.contains(username.toLower())
    )
    icon i_liked ? "favorites-full" "favorites" 0.8
    if onclick (
      feed[count].likes ??= []
      if i_liked (
        feed[count].likes = feed[count].likes.delete(username.toLower())
        local rating = 0
      ) else (
        feed[count].likes = feed[count].likes.append(username.toLower())
        local rating = 1
      )
      set_rating post.id rating
      posts.feed = feed
    )

    change_x total_likes.len * -8 - 20
    text total_likes 8
    
    change_x -40
    icon "w 4 cutcircle 0 -5 10 4 45 w 4.1 line 10 -10 10 -5 line 0 5 -10 5 cont -5 0 line -10 5 -5 10" 0.7
    local total_replies = (post.replies ?? []).len
    change_x total_replies.len * -8 - 20
    text total_replies 8
  )
  if post.attachment == "" (
    local offset = 0
  ) else (
    local offset = 10
    loc -2 2 -70 y
    icon "image" 0.8
    image post.attachment 100 100
    if clicked (
      overlay = post.attachment
    )
  )
  loc 2 2 20 y + 20
  text post.content.replace("\n"," ").wrapText(w / 9 - 1 - offset) 9
)

def "render_main" "feed" (
  local ih = 180
    
  len = feed.len
  count = (scroll_yfeed // ih).clamp(0,100)
  y = scroll_yfeed % ih - (ih / 2)
  
  local w = frame.width - 25
  loop (window_height // ih + 1).clamp(0,len) (
    count ++
    if count > len (
      return
    )
    local h = ih - 20
    
    render_post w h feed[count] (h / 2 - 20) url.startsWith("Post_")
    y -= ih
  )
)

def "side_button" "this.icn, this.txt, this.callback" (
  square 450 0 30 0 1
  if mouse_touching (
    cursor "pointer"
    if onclick (
      return callback()
    )
  )
  icon icn 0.7 : c#txtc
  change_x icn == "" ? -15 30
  
  text txt 10 : c#txtc
)

def "claw_input" "y, w, onsend, message" (
  goto x y
  square w 90 10 : c#window_colour

  change_y 25
  input w - 15 23 "claw" message
  input_claw.trim(0,100)
  change w / -2 + 10 -50
  text input_claw.len ++ "/100" 8 : c#txtc

  change_x 40
  text "Click Here To Paste An Image URL" 8
  if onclick (
    attachment = clipboard
    if attachment.startsWith("https://").not (
      attachment = ""
      say "Invalid, must be https:// image"
    )
  )
  if attachment != "" (
    image attachment 30 30 : chx#30
  )
  goto 115 + (w / 2) y - 25
  icon "send" 0.8
  if onclick (
    onsend()
  )
)

mainloop:
if url.startsWith("Post_") (
  theme = user.theme
  post_id = url.split("_")[2]
) else if url == "feed" or url.startsWith("following_feed") (
  theme = user.theme
) else (
  theme = posts.data.theme
)
prim = theme.primary
seco = theme.secondary
tert = theme.tertiary
txtc = theme.text
window_accent = theme.accent
window_colour = theme.background

w = window

glass:frame w.left w.top w.right w.bottom
  goto 0 0
  pen "size" w.width + w.height
  c window_colour
  pen "opacity" 50
  pen "down"
  pen "up"
frame "clear"

top = w.top - 170
x = w.width > 1100 ? 135 0

if posts.got (
  c prim
  len = posts.feed.len
  
  if url == "feed" (
    claw_input w.top - 110 790 def() -> (
      send_post input_claw attachment
      input_claw = ""
      attachment = ""
      posts.got = false
      posts.feed = []
    ) "Type your post..."
  ) else if url.startsWith("Post_") (
    top -= 190
    
    frame x - 408 w.top - 50 x + 408 top - 110 (
      y = -100
      render_post 790 170 posts.data 65
    )
    
    claw_input w.top - 300 790 def() -> (
      c (api ++ "reply?id=" ++ posts.data.id ++ "&auth=" ++ token ++ "&content=" ++ encodeURIComponent(input_claw)).getAsync()
      input_claw = ""
      attachment = ""
    ) "Reply to this post..."
  ) else (
    top = w.top - 60
  )
  
  c prim

  frame x - 408 top x + 408 w.bottom len * 180 "feed" (
    render_main posts.feed
  )
) else if url.startsWith("profile?name=") and users.contains(url.split("=").last()) (
  name = url.split("=").last()
  posts.feed = users[name].posts
  posts.data = users[name].delete("posts")
) else (
  posts.feed = (api ++ url).getAsync()
  if posts.feed != "Loading" (
    posts.got = true
    if url.startsWith("profile?") (
      posts.data = posts.feed.delete("posts")
      posts.feed = posts.feed.posts
    )
  )
)

if user_profile == "" (
  user_profile = (api ++ "profile?name=" ++ username).getAsync()
  if user_profile == "Loading" (
    user_profile = ""
  )
)

x = w.left + 25

goto w.left + 120 0
square 300 w.height 10 : c#window_colour

if url.startsWith("Post_") (
  loc 2 2 25 -75
  log posts.data.user
  side_button "image https://avatars.rotur.dev/" ++ posts.data.user + "40" posts.data.username def() -> (
    load_profile username
  )

  set_x x : chy#-40
  side_button "" "Followers:" + posts.data.followers def() -> ()

  set_x x : chy#-30
  side_button "" "Following:" + posts.data.following def() -> ()

  set_x x : chy#-50
  side_button "left-arrow" "Back To Feed" def() -> (
    posts.got = false
    url = "feed"
  )
) else (
  if url.startsWith("profile").not() (
    loc 2 2 25 -75
    side_button "image" + user_icon + "40" "My Profile" def() -> (
      load_profile username
    )

    set_x x : chy#-40
    side_button "" "Followers:" + user_profile.followers def() -> ()

    set_x x : chy#-30
    side_button "" "Following:" + user_profile.following def() -> ()
  ) else (
    loc 2 2 25 -75
    side_button "image" + posts.data.pfp + "40" posts.data.username def() -> (
      load_profile username
    )

    set_x x : chy#-40
    side_button "" "Followers:" + posts.data.followers def() -> ()

    set_x x : chy#-30
    side_button "" "Following:" + posts.data.following def() -> ()

    set_x x : chy#-50
    side_button "left-arrow" "Back To Feed" def() -> (
      posts.got = false
      url = "feed"
    )
  
    set_x x : chy#-50
    if posts.data.followed (
      side_button "tick" "Unfollow" + posts.data.username def() -> (
        local url = api ++ "/unfollow?auth=" ++ token ++ "&username=" ++ posts.data.username
        posts.data.followed = false
        return url.getAsync()
      )
    ) else (
      side_button "tick" "Follow" + posts.data.username def() -> (
        local url = api ++ "/follow?auth=" ++ token ++ "&username=" ++ posts.data.username
        posts.data.followed = true
        return url.getAsync()
      )
    )
  )
)

set_x x : chy#-50
side_button "reload" "Reload" def() -> (
  posts.got = false
  
  if url.startsWith("Post_") (
    posts.got = true
    posts.feed = posts.data.replies
  )
)

goto 0 w.top - 20
square w.width 40 10 : c#window_colour

c txtc
loc 2 2 10 -20
text "Claw" 10

loc 9999 2 20 -20
text "Claw Feed" 10
if onclick (
  posts.got = false
  url = "feed"
)

loc 9999 2 160 -20
text "Following" 10
if onclick (
  posts.got = false
  url = "following_feed?auth=" ++ token
)

if overlay != "" (
  goto 0 0
  c window_colour
  pen "opacity" 90
  pen "size" window.width + window.height
  pen "down"
  pen "up"
  image overlay 600
  if mouse_ondown (
    overlay = ""
  )
)

import "win-buttons"
