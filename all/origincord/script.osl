base = {"op":2,"d":"","s":null,"t":null}
// base packet
window "dimensions" 1000 600
properties = {"$os":"originOS","$browser":"originOS","$device":"originOS"}
properties."$browser" = system_browser.destr
properties."$device" = system_os.destr
// get system properties
loaded = []
d = {"token":"","intents":513}
d."properties" = properties

window "responsive" true
// make the window run even when unfocused

def "connect"
  discord = "gateway.discord.gg/?v=9&encoding=json:443".newWebsocket()
  until discord.wsOpen() (
  )
  log "connected"
endef

def "login" "token"
  temp = base
  status = "online"
  dat = d
  dat."token" = token.destr
  temp."d" = dat
  discord.wsSend(temp)
  heartbeat_msg = {"op":1,"d":null}
  last_heartbeat = timer + 41.25
endef

def "heartbeat"
  if last_heartbeat < timer (
    last_heartbeat = timer + 41.25
    discord.wsSend(heartbeat_msg)
  )
endef

def "send_message" "content, channel"
  if ( "https://origin.milosantos.com/discord/sendmessage.php?token=" ++ token ++ "&channelid=" ++ channel ++ "&content=" ++ content ).get()
endef

def "dm_user" "content, user_id"
  channel = ( "https://proxy.milosantos.com/turbowarpbot/createdm.php?recipient_id=" ++ user_id ++ "&token=" ++ token ).get()
  log channel
  send_message content channel
endef

import "window_tools"

originCord_icn = "c #5865f2 w 20 dot 0 0 c #fff w 3 line 3 2 5 -2 cont 3 -3 cont 2 -2 cont -2 -2 cont -3 -3 cont -5 -2 cont -3 2.5 cont -2 2.5 cont -1 1.7 cont 1 1.7 cont 2 2.5 cont 3 2.5 w 4.5 line -2 0 2 0 c #5865f2 w 2.5 dot 2 -1 dot -2 -1"

seticon originCord_icn
permission "request" "notifications"

save "originCord@mistium" "set_directory"
if save_data == "Failed (Permissions)" (
  say "OriginCord cannot load without save admin"
  permission "request" "save admin"
)
save "token.txt" "exists"
if save_data (
  save "token.txt" "get"
  token_in = save_data.str
) else (
  token_in = "Enter A Discord Bot Token".ask
  if token_in != "" (
    save "token.txt" "set" token_in.str
  ) else (
    window "stop"
  )
)
connect
wait 0.6
login token_in
wait 0.6
activity_data = [{"name":"originCord","type":1}]

messages = {}
previous_author = {}

def "async_msg" "content, channel"
  link = "https://origin.milosantos.com/discord/sendmessage.php?token=" ++ token ++ "&channelid=" ++ channel ++ "&content=" ++ content.str
  if origin."version" < 501 (
    network "get" link
  ) else (
    network "getasync" link
  )
endef

def "handle_message" "msg, ping"
  msg_cha = messages.[channel]
  if msg_cha.istype("Array").not (
    messages.[channel] = []
    msg_cha = []
  )
  name = author."username"
  attachments = msg."attachments"
  if attachments.len > 0 (
    out = []
    each "i" "attachment" attachments (
      if attachment."content_type" == "image/png" (
        out.append(attachment)
      )
    )
    attachments = out
  )
  pfp = "https://cdn.discordapp.com/avatars/" ++ author."id" ++ "/" ++ author."avatar" ++ ".png"
  authid = author."id"
  if previous_author.[channel] != authid (
    msg_cha.append("")
    previous_author.[channel] = authid
    message = []
    message.append(name)
    message.append("")
    message.append(pfp)
    message.append(false)
    message.append("")
    msg_cha.append(message)
  )
  content.split(newline)
  each "i" "item" content (
    message = []
    message.append("")
    message.append(item)
    message.append("")
    message.append(ping)
    message.append(attachments)
    msg_cha.append(message)
  )
  messages.[channel] = msg_cha
endef

def "draw_guilds"
  c window_colour
  len = guilds.len
  frame window."left" window."top" - 40 window."left" + 50 window."bottom" len - 0.5 * 50 "guild"
  count = scroll_yguild // 50
  loop ( window_height // 50 ).clamp(0,len) (
    count ++
    y = count - 0.5 * -50 + scroll_y
    guild = guilds.arr[count]
    loc 2 2 0 y
    if guild == current_guild (
      square 0 30 10 : c#fff
      loc 999 2 0 y
    ) else (
      loc 999 2 0 y
      square 40 40 10 0 1
      if clicked (
        current_guild = guild
      )
    )
    image guild.arr[2] 40 40
  )
  frame "clear"
endef

def "draw_channels" "cur"
  c #333
  len = cur.len
  frame window."left" + 50 window."top" - 40 window."left" + 360 window."bottom" + 50 len - 0.5 * 40
  goto 0 0
  square frame_width frame_height 10 : c#111
  count = scroll_y // 40
  loop ( window_height // 40 ).clamp(0,len) (
    count ++
    y = count - 0.5 * -40 + scroll_y
    chnl = cur.arr[count]
    loc 999 2 0 y
    if chnl == current_channel "c #333" else "c #222"
    square frame_width - 15 25 10
    id = chnl."id"
    if clicked (
      current_channel = chnl
      if loaded.contains(id).not (
        link = "https://origin.milosantos.com/discord/channelmessages.php?channelid=" ++ id ++ "&limit=100&token=" ++ token
        data = link.get().JsonClean()
        data.reverse
        channel = id
        each "int" "load_msg" data (
          author = load_msg."author"
          content = load_msg."content"
          handle_message load_msg false
        )
        loaded.append(id)
      )
      ping_channels.delete(id)
    )
    loc 2 2 20 y
    if ping_channels.contains(id) (
      change_x 10
      icon "c #de0a26 w 10 dot 0 0 c #fff w 2 line 0 3 0 0 dot 0 -3" 2
      change_x 10
    )
    text chnl."name" 10 : c#fff
  )
  frame "clear"
endef

def "draw_chat" "cur_msgs"
  img_display = ""
  c #333
  len = cur_msgs.len
  frame window."left" + 360 window."top" - 40 window."right" window."bottom" + input_offset len * 30 "chat"
  count = scroll_ychat // 30
  y = 0
  loop ( window_height // 30 ).clamp(0,len) (
    count ++
    msg = cur_msgs.arr[count]
    y = count * -30 + scroll_ychat
    loc 999 2 0 y
    if msg.arr[4] == true (
      square frame_width 20 10 : c#625228
    ) else (
      square frame_width 20 10 0 1
    )
    hover = mouse_touching
    loc 2 2 20 y
    author = msg."author"
    if msg.arr[1].str != "" (
      image msg.arr[3].str 30 30
      change_x 20
      text msg.arr[1].str 12 : c#fff
    ) else (
      change_x 20
      text msg.arr[2].str 10 : c#fff
      if msg.arr[5].len > 0 (
        text "(hover to view attachment)" 7 : c#aaa
        if hover (
          img_display = msg.arr[5].arr[1]."url"
        )
      )
    )
  )
  frame "clear"
endef

guilds = []

mainloop:
heartbeat

if discord.wsHasnew() (
  msg = discord.wsGetnext()
  cmd = msg."t"
  if cmd == "ready" (
    discord_user = msg."d"."user"
    discord_id = discord_user."id"
    discord_pfp = "https://cdn.discordapp.com/avatars/" ++ discord_id ++ "/" ++ discord_user."avatar" ++ ".png"
    discord_username = discord_user."username"
  )
  if cmd == "GUILD_CREATE" (
    guild = msg."d"
    channels = guild."channels"
    out_channels = []
    categories = {}
    ping_channels = []
    loop channels.len (
      out_channels.append("")
    )
    each "i" "channel" channels (
      position = channel."position"
      out_channels.[position] = channel.JsonClean()
    )
    channels = out_channels
    name = guild."name"
    id = guild."id"
    icon = "https://cdn.discordapp.com/icons/" ++ guild."id" ++ "/" ++ guild."icon" ++ ".png?size=96"
    temp = []
    temp.append(name)
    temp.append(icon)
    temp.append(channels)
    temp.append(id)
    guilds.append(temp)
  )
  if cmd == "MESSAGE_CREATE" (
    msgd = msg."d"
    channel = msgd."channel_id"
    if loaded.contains(channel) (
      content = msgd."content"
      author = msgd."author"
      ping = false
      pings = content.regex("/<@\d+>/gm")
      if pings.len > 0 (
        my_ping = "<@" ++ discord_id ++ ">"
        each "i" "ping" pings (
          if ping == my_ping (
            ping_loc = channels.getall("id").index(channel)
            notify originCord_icn "You got pinged by" + author."username" "in" + channels.[ping_loc]."name"
            ping_repl = "@" ++ discord_username
            content.replace(my_ping,ping_repl)
            ping_channels.append(channel)
            ping = true
          )
        )
      )
      handle_message msg."d" ping
    )
  )
)
if file_dropped != 0 (
  file "open" file_dropped "onlyaccess"
  temp = []
  file "get" 4
  temp.append(data)
  file "get" 2
  temp2 = data
  file "get" 1
  name = temp2 ++ data
  temp.append(name)
  attachments.append(temp)
)
if attachments.len == 0 (
  input_offset = 40
) else (
  inpu_offset = 100
)
loc 999 2 0 -20
square window_width 30 10 : c#333
loc 2 2 20 -20
icon originCord_icn 1
change_x 20
text "originCord" 10 : c#fff
draw_guilds
draw_channels current_guild.[3]
channel_id = current_channel."id"
draw_chat messages.[channel_id]
loc 2 -2 205 25
square 300 30 10 : c#222
change_x -135
image discord_pfp 30 30
change_x 30
text discord_username 9 : c#fff
if img_display != "" (
  goto 0 0
  w = img_display.imageinfo("width")
  h = img_display.imageinfo("height")
  temp = window_width - 50 / w * 70
  temp2 = window_height - 50 / h * 70
  if temp2 > temp (
    stretch [].append(temp).append(temp)
  ) else (
    stretch [].append(temp2).append(temp2)
  )
  image img_display.str
)
w = window_width - 380
loc 999 -2 180 25
c #222
square w 30 10
input w 30 "message"
import "win-buttons"
if "enter".pressed and input_message != "" (
  log current_guild
  send = input_message
  spl = send.split(" ")
  if send.startsWith("!ocord") (
    if spl.[2] == "status" (
      status = {"op":3,"d":{"since":null,"status":"online","afk":false}}
      temp = status."d"
      temp."status" = spl.[3]
      temp."activities" = activity_data
      status."d" = temp
      discord.wsSend(status)
    )
    if spl.[2] == "bgcolour" (
      window_colour = spl.[3]
    )
  ) else (
    async_msg send.to("url") channel_id
  )
  input_message = ""
)
