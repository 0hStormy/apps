yt_icn = "c #111 w 10 square 0 0 5 5 c #F00 w 8 square 0 0 5 5 dot 0 0 w 3 c #FFF line 3 0 -2.5 3 cont -2.5 -3 cont 3 0 dot 0 0"
page = "search"
term = ""
search = ""

player = ""

proxy = "https://apps.mistium.com/cors?url="

prim = user.theme.primary
seco = user.theme.secondary
tert = user.theme.tertiary
txtc = user.theme.text
window_colour = user.theme.background

window.width = 800
window.height = 500

drag_box = [[-2,2,-140,0],[-2,2,-90,-30]]

mainloop:

window "set_dragbox" drag_box
switch page (
  case "search"
    drag_box = [[-2,2,-140,0],[-2,2,-90,-30]]
    window "responsive" false
    goto -70 window.top - 20
    square window.width - 160 20 10 : c#prim
    change_x 10
    input window.width - 200 26 "search" "Search Youtube"
    if selected_input == "search" and "enter".onpress (
      search = ""
      term = input_search
    )
    loc 2 2 20 -20
    icon yt_icn 1
    if search == "" and term != "" (
      formatted = term.replace("+","%2B").replace(" ","+")
      formatted = "https://www.youtube.com/results?search_query=" ++ formatted
      search_url = proxy ++ formatted.to("url")
      data = search_url.getAsync()
      if data != "loading" (
        out = data.regex("/\/watch\?v=([a-zA-Z0-9_-]{11})/g")
        search = []
        each i result out (
          result.trim(10,-1)
          if search.contains(result).not (
            search.append(result)
          )
        )
        out = []
      )
    ) else (
      c prim
      frame window.left window.top - 40 window.right window.bottom search.len * 200
      i = (scroll_y / 200).floor()
      loop (frame_height / 200 + 1).clamp(0,search.len) (
        i ++
        result = search[i]
        
        y = i - 0.5 * -200 + scroll_y
        if result.isType("string") (
          result = ("https://www.youtube.com/oembed?url=http%3A//youtube.com/watch%3Fv%3D" ++ result ++ "&format=json").getAsync()
        )

        if result != "loading" (
          loc 9999 2 0 y
          square frame.width - 25 170 15 : c#prim
          if onclick (
            page = "player"
            player_id = result
            p_url = "https://www.youtube.com/embed/" ++ result
            if player == "" (
              player = p_url.iframeNew()
            ) else (
              player.iframeRedirect(p_url)
            )
            player_info = data
            player.iframeShow()
          )
          loc 2 2 152 y
          image proxy ++ result.thumbnail_url 280 170
          loc 2 2 330 y + 30
          text result.title 10 : c#txtc
          loc 2 2 330 y - 20
          text result.author_name 8
          search[i] = result
        )
      )
      frame "clear"
    )
    if term == "" (
       goto 0 0
       icon yt_icn 4
       txt = "Youtube"
       goto txt.len * -5 -60
       text txt 10 : c#txtc
    )
    break
  case "player"
    drag_box = [[2,2,80,0],[-2,2,-90,-30]]
    window "responsive" true
    player.iframeShow()
    goto -45 window.top - 20
    square window.width - 110 20 10 : c#prim
    loc 2 2 20 -20
    icon "left-arrow" 0.7 : c#txtc
    if onclick (
      page = "search"
      player.iframeHide()
    )
    icon yt_icn 0.7 : chx#30
    text player_info.title 8 : chx#30
    player.iframeResize(window_width - 20,window_height - 60)
    player.iframeGoto(0,-20)
    break
)

import "win-buttons"
