seticon "image https://raw.githubusercontent.com/Mistium/Origin-OS/main/OSL%20Programs/apps/games/assets/2048.png 25"
window "dimensions" 450 500
window "resizable" false
window "show"
tiles = [
  [0,0,0,0],
  [0,0,0,0],
  [0,0,0,0],
  [0,0,0,0]
]
colours = {
  "2": "#eee4da",
  "4": "#ede0c8",
  "8": "#f2b179",
  "16": "#f59563",
  "32": "#f67c5f",
  "64": "#f65e3b",
  "128": "#edcf72",
  "256": "#edcc61",
  "512": "#edc850",
  "1024": "#edc53f",
  "2048": "#edc22e",
  "4096": "#ffbf00",
  "8192": "#ff8000",
  "16384": "#ff4000",
  "32768": "#ff0000",
  "65536": "#e60000",
  "131072": "#cc0000",
  "262144": "#b30000",
  "524288": "#990000",
  "1048576": "#800000"
}
window "show"
def "generate"
  empty_tiles = []
  count = 0
  loop 4 (
    count ++
    count2 = 0
    loop 4 (
      count2 ++
      if tiles.arr[count].arr[count2] == 0 (
        data = [].append(count).append(count2)
        empty_tiles.append(data)
      )
    )
  )
  emptlen = empty_tiles.len
  if emptlen > 0 (
    if [0,9].random < 9 "random_num = 2" else "random_num = 4"
    rand = [1] + emptlen
    rand = rand.random
    rand = empty_tiles.arr[rand]
    rand1 = rand.[1]
    rand2 = rand.[2]
    temp = tiles.arr[rand1]
    temp.[rand2] = random_num
    tiles.[rand1] = temp
  )
endef

generate
generate
generate

def "rotate"
  out = []
  loop 4 (
    count2 = 1
    loop 4 (
      cur = []
      count = 4
      loop 4 (
        data = tiles.arr[count].arr[count2]
        cur.append(data)
        count --
      )
      out.append(cur)
      count2 ++
    )
  )
  tiles = out
endef
def "move_left"
row = 0
loop 4 (
  row ++
  loop 4 (
    count = 1
    loop 3 (
      count ++
      count2 = count - 1
      cur_row = tiles.arr[row]
      
      if cur_row.arr[count2] == 0 and ( cur_row.arr[count] > 0 ) (
        cur_row.[count2] = cur_row.arr[count]
        cur_row.[count] = 0
      )
      if cur_row.arr[count] == cur_row.arr[count2] (
        cur_row.[count2] *= 2
        cur_row.[count] = 0
      )
      tiles.[row] = cur_row
    )
  )
)
endef
def "move_right"
row = 0
loop 4 (
  row ++
  loop 4 (
    count = 0
    loop 4 (
      count ++
      count2 = count + 1
      cur_row = tiles.arr[row]
      
      if cur_row.arr[count2] == 0 and ( cur_row.arr[count] > 0 ) (
        cur_row.[count2] = cur_row.arr[count]
        cur_row.[count] = 0
      )
      if cur_row.arr[count] == cur_row.arr[count2] (
        cur_row.[count2] *= 2
        cur_row.[count] = 0
      )
      tiles.[row] = cur_row
    )
  )
)
endef

def "left"
  move_left
  generate
endef

def "right"
  move_right
  generate
endef

def "up"
  rotate
  move_right
  rotate
  rotate
  rotate
  generate
endef

def "down"
  rotate
  move_left
  rotate
  rotate
  rotate
  generate
endef

mainloop:
count = 0
total = 0
loop 4 (
  count ++
  count2 = 0
  loop 4 (
    count2 ++
    current = tiles.arr[count].arr[count2]
    goto count2 - 2.5 * 100 count - 2.5 * -100 + 25
    total += current.int
    c colours.key(current)
    square 80 80 10
    change_x current.len * -9
    text current 18 : c#000
  )
)
loc 999 -2 0 25
square 500 60 10 : c#333
loc 2 -2 30 30
square 20 20 10 : c#555
if mouse_touching (
  cursor "pointer"
  if clicked (
    window "stop"
  )
)
icon "close" 0.6 : c#fff
change_x 30
text "Total:" + total 10
if "a".onpress or "left arrow".onpress (
  left
)
if "d".onpress or "right arrow".onpress (
  right
)
if "w".onpress or "up arrow".onpress (
  up
)
if "s".onpress or "down arrow".onpress (
  down
)
